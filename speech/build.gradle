apply plugin: 'com.android.library'
apply plugin: 'com.google.protobuf'
android {
    def sdk = rootProject.ext.sdk
    compileSdkVersion sdk.compileSdkVersion

    defaultConfig {
        minSdkVersion sdk.minSdkVersion
        targetSdkVersion sdk.targetSdkVersion

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

protobuf {
    protoc {
        artifact = rootProject.ext.protoc
    }

    plugins {
        java {
            artifact = rootProject.ext.protoc
        }
    }

    generateProtoTasks {
        all().each { task ->
            task.plugins {
                java {}
            }
        }
    }
}

dependencies {
    api project(':commons')
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    def deps = rootProject.ext.deps
    implementation deps.appcompat
    implementation deps.okHttp
    implementation deps.retrofit
    implementation deps.gsonConverter
    implementation deps.uRestAdapter
    implementation deps.signInterceptor
    implementation deps.authInterceptor
    implementation 'com.squareup.okhttp3:logging-interceptor:3.4.1'
    implementation(group: 'com.ubtrobot.ulog', name: 'logger-android', version: '0.0.1')

    testImplementation deps.junit
    androidTestImplementation deps.espressoCore
    androidTestImplementation deps.runner
}

task jarFramework(
        type: Jar,
        dependsOn: ['assembleRelease']) {
    archiveName = "speech.jar"

    exclude('**/R.class')
    exclude('**/R$*.class')
    exclude('**/BuildConfig.class')
    exclude('**/BuildConfig$*.class')
    exclude('android/')

    exclude('com/ubtrobot/commons/')
    exclude('com/ubtrobot/speech/ipc/master')
    exclude('com/ubtrobot/speech/sal/')

    from 'build/intermediates/classes/release'
}